buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'findbugs'
    id 'pmd'
    id 'checkstyle'
    id 'jacoco'
}

apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'

apply from: 'gradle/validation.gradle'
apply from: 'gradle/coverage.gradle'

group 'com.github.slamdev'
version '1.0.0'

sourceCompatibility = 1.8

idea {
    project {
        ipr {
            withXml { provider ->
                def projectXml = provider.asNode()
                def compilerConfiguration = projectXml.component.find { component ->
                    component.'@name' == 'CompilerConfiguration'
                }
                def currentAnnotationProcessing = compilerConfiguration.annotationProcessing
                currentAnnotationProcessing.replaceNode {
                    annotationProcessing {
                        profile(name: 'Default', default: true, enabled: true) {
                            processorPath(useClasspath: true)
                        }
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        output.resourcesDir = 'build/classes/main'
        output.classesDir = 'build/classes/main'
    }
    test {
        output.resourcesDir = 'build/classes/test'
        output.classesDir = 'build/classes/test'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

dependencies {
    compile 'org.jboss.weld.se:weld-se-core:2.4.0.CR1'
    runtime 'org.jboss:jandex:2.0.3.Final'
    compile 'org.slf4j:slf4j-api:1.7.21'
    runtime 'ch.qos.logback:logback-classic:1.1.7'
    provided 'org.projectlombok:lombok:1.16.10'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
